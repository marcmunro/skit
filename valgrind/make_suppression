#! /usr/bin/awk -f
# Create suppression file output based on standard input
# Each suppression should be created from a test in the Suppressions
# test suite run from skit_test.  Each test should print to stdout
# a line of the form:
# SUPPRESSION: <name> <pattern>
# Where name will be the name of the suppression file and the suppressions
# contained within it, and pattern will match a function in the pattern file
# Lines following and including pattern will be removed from the suppression
# output.

BEGIN {
    printing = 0
    ok_to_go = 0
}
/^{/ {
    printing = ok_to_go
}
/^SUPPRESSION/ {
    ok_to_go = 1
    title = $2
    pattern = $3
    count = 1
    next
}
/^==/ {
    next
}
/insert/ {
    if (printing) {
	print "   " title "_" count++
    }
    next
}
/fun:/ {
    if (printing) {
	if ($1 ~ pattern) {
	    print "}"
	    printing = 0
	}
    }
}
(printing)
/^}/ {
    printing = 0
}
