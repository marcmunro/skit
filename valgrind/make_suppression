#! /usr/bin/awk -f
# Create suppression file output based on standard input
# Each suppression should be created from a test in the Suppressions
# test suite run from skit_test.  Each test should print to stdout
# lines of the form:
# SUPPRESSION: <name> <pattern>
# Where name will be the name of the suppression file and the suppressions
# contained within it, and pattern will match a function in the pattern file
# Lines following and including the patterns will be removed from the
# suppression output.

function add_pattern(pattern) {
    patterns[pattern_idx++] = pattern
}

function matched_pattern(line, i) {
    for (i = 0; i < pattern_idx; i++) {
	if (line ~ patterns[i]) {
	    return 1
	}
    }
    return 0
}

BEGIN {
    printing = 0
    count = 1
    pattern_idx = 0
}
/^{/ {
    printing = ok_to_go
}
/^SUPPRESSION/ {
    ok_to_go = 1
    title = $2
    add_pattern($3)
    next
}
/^==/ {
    next
}
/insert.*suppression/ {
    if (printing) {
	print "   " title "_" count++
    }
    next
}
/fun:/ {
    if (printing) {
	if (matched_pattern($1)) {
	    print
	    print "}"
	    printing = 0
	}
    }
}
(printing)
/^}/ {
    printing = 0
}
